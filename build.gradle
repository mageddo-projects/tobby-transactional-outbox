buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "net.researchgate:gradle-release:2.8.1"
  }
}

allprojects {

  apply plugin: "java-library"

  repositories {
    mavenLocal()
    jcenter()
  }

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  dependencies {

    implementation("com.google.dagger:dagger:2.33")
    annotationProcessor("com.google.dagger:dagger-compiler:2.33")

    compileOnly("org.projectlombok:lombok:1.18.16")
    annotationProcessor("org.projectlombok:lombok:1.18.16")

    implementation ("org.apache.kafka:kafka-clients:0.11.0.3")

    testImplementation("org.junit.jupiter:junit-jupiter:5.7.0")
    testImplementation("org.mockito:mockito-junit-jupiter:3.7.7")
    testImplementation(project(":templates"))
    testImplementation("ch.qos.logback:logback-classic:1.2.3")

    testCompileOnly "org.projectlombok:lombok:1.18.16"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.16"
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "skipped", "failed"
    }
    failFast = true
  }

  compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

}

apply plugin: "maven"
apply plugin: "net.researchgate.release"
apply from: "${rootDir}/nexus.gradle"


release {

  project.ext.set("release.useAutomaticVersion", true)
  git {
    requireBranch = "main"
  }
  failOnCommitNeeded = false
  failOnPublishNeeded = false
  failOnUnversionedFiles = false
  buildTasks = []
}

confirmReleaseVersion.doLast {
  def f = file("${project.projectDir}/README.md")
  def text = f.text
  f.withWriter { w ->
    w << text.replaceAll("(:\\ )*\\d+\\.\\d+\\.\\d+", "\$1${version}")
  }
}

dependencies {

  compileOnly(project(":adapter"))

  testImplementation group: "org.flywaydb", name: "flyway-core", version: "7.6.0"
  testImplementation("org.jdbi:jdbi3-core:3.0.0")
  testImplementation("io.zonky.test:embedded-postgres:1.3.1")
  testRuntimeOnly enforcedPlatform("io.zonky.test.postgres:embedded-postgres-binaries-bom:11.13.0")

  testImplementation group: "org.postgresql", name: "postgresql", version: "42.2.19"
  testImplementation group: "org.hsqldb", name: "hsqldb", version: "2.5.1"
  testImplementation group: "com.h2database", name: "h2", version: "1.4.200"
  testImplementation group: "com.zaxxer", name: "HikariCP", version: "4.0.3"

  testImplementation group: 'com.oracle.database.jdbc', name: 'ojdbc10', version: '19.10.0.0'

}

